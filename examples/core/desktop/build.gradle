project.ext.mainClassName = "com.github.xpenatan.gdx.examples.desktop.Main"

project.ext.assetsDir = new File("/assets");

dependencies {
    implementation "com.badlogicgames.gdx:gdx-platform:$LibExt.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$LibExt.gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$LibExt.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$LibExt.gdxVersion:natives-desktop"
    implementation project(":examples:core:core")
}

tasks.register('runCoreDesktop', JavaExec) {
    dependsOn classes
    setGroup("examples-desktop")
    setDescription("Run core example")
    mainClass.set(project.mainClassName)
    setClasspath(sourceSets.main.runtimeClasspath)
    workingDir = project.assetsDir

    if (org.gradle.internal.os.OperatingSystem.current() == org.gradle.internal.os.OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('dist', Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}