plugins {
    id "org.gretty" version "3.0.6"
}

gretty {
    servletContainer = 'jetty9'
    extraResourceBase 'webapp'
}

sourceSets.main.java.srcDirs = ["src/main/java/"]
project.ext.mainClassName = "com.github.xpenatan.gdx.examples.box2d.Build"

dependencies {
    implementation project(":examples:box2d:core")
    implementation "com.badlogicgames.gdx:gdx:$project.gdxVersion"

    // Use snapshots
    implementation "com.github.xpenatan.gdx-teavm:backend-web:1.0.0-SNAPSHOT"
    implementation "com.github.xpenatan.gdx-teavm:backend-teavm:1.0.0-SNAPSHOT"
    implementation "com.github.xpenatan.gdx-teavm:gdx-box2d-teavm:1.0.0-SNAPSHOT"

    // Or source projects
//    implementation project(":backends:backend-web:backend-web")
//    implementation project(":backends:backend-teavm:backend-teavm")
//    implementation project(":extensions:gdx-box2d:gdx-box2d-teavm")
}

clean.doFirst {
    def webappPath = projectDir.toString() + "/webapp/"
    if(new File(webappPath).exists()) {
        project.delete(files(webappPath))
    }
}

task buildExampleBox2D(dependsOn: classes, type: JavaExec) {
    setGroup("teavm")
    setDescription("Build Box2D example")
    mainClass.set(project.mainClassName)
    setClasspath(sourceSets.main.runtimeClasspath)
}

task runBox2D(dependsOn: [':examples:box2d:teavm:clean', ':examples:box2d:teavm:buildExampleBox2D', ':examples:box2d:teavm:jettyRun']) {
    setGroup("examples-teavm")
    setDescription("Run Box2D example")
}

runBox2D.shouldRunAfter ":examples:box2d:teavm:clean"