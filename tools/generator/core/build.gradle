plugins {
    id 'java'
    id 'signing'
}

group = project.groupId

def module_name = "generator-core"

sourceSets.main.java.srcDirs = ["src/main/java/"]

configurations {
    includeDep
    implementation.extendsFrom includeDep
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$project.gdxVersion"
    includeDep "com.github.xpenatan.jDear-Imgui:core:$project.jDearImguiVersion"
    includeDep "com.github.xpenatan.jDear-Imgui:gdx:$project.jDearImguiGdxVersion"
    includeDep "com.github.xpenatan.jDear-Imgui:core-platform:$project.jDearImguiVersion:natives-desktop"

    includeDep project(":backends:backend-teavm")
//    includeDep project(":extensions:gdx-freetype-teavm")
//    includeDep project(":extensions:gdx-bullet:gdx-bullet-teavm")
//    includeDep project(":extensions:gdx-box2d:gdx-box2d-teavm")
    includeDep 'org.eclipse.jetty:jetty-server:11.0.13'
    includeDep 'org.eclipse.jetty:jetty-webapp:11.0.13'
}

tasks.register('fromClasses', Jar) {
    from(sourceSets.main.output) {
    }
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            version project.gdxTeaVMVersion
            project.version = project.gdxTeaVMVersion
            artifactId module_name
            artifact fromClasses
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = "gdx-teaVM"
                description = "Tool to generate libgdx to javascript using teaVM"
                url = "https://github.com/xpenatan/gdx-teavm"
                developers {
                    developer {
                        id = "Xpe"
                        name = "Natan"
                    }
                }
                scm {
                    connection = "scm:git:git://https://github.com/xpenatan/gdx-teavm.git"
                    developerConnection = "scm:git:ssh://https://github.com/xpenatan/gdx-teavm.git"
                    url = "http://https://github.com/xpenatan/gdx-teavm/tree/master"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.includeDep.allDependencies.each {
                        def dependencyNode = dependencies.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

signing {
    def ver = project.version.toString()
    def isSnapshot = ver.toUpperCase().contains("SNAPSHOT")
    if(!isSnapshot) {
        def signingKey = System.getenv("signingKey")
        def signingPassword = System.getenv("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}